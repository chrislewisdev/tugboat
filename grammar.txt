program         -> declaration* EOF;

declaration     -> funDecl | varDecl | statement;

funDecl         -> "fn" function;
function        -> IDENTIFIER "(" parameters? ")" block;
parameters      -> IDENTIFIER ( "," IDENTIFIER )*;
varDecl         -> "u8" IDENTIFIER ("=" expression)? ";";

statement       ->  haltStmt | assignStmt | exprStmt | forStmt | ifStmt | returnStmt | whileStmt | block;
haltStmt        ->  "halt" ";" ;
assignStmt      ->  IDENTIFIER "=" assignment ";" ;
exprStmt        ->  expression ";" ;
forStmt         ->  "for" "(" (varDecl | exprStmt | ";")
                    expression? ";"
                    expression? ")" statement;
ifStmt          ->  "if" "(" expression ")" statement ("else" statement)?;
returnStmt      ->  "return" expression? ";" ;
whileStmt       ->  "while" "(" expression ")" statement;
block           ->  "{" statement* "}";

expression      ->  logic_or;
logic_or        ->  logic_and ( "||" logic_and )*;
logic_and       ->  equality ( "&&" equality )*;
equality        ->  comparison ( ( "!=" | "==" ) comparison )* ;
comparison      ->  term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term            ->  factor ( ( "-" | "+" ) factor )* ;
factor          ->  unary ( ( "/" | "*" ) unary )* ;
unary           ->  ( "!" | "-" ) unary
                    | call ;
call            ->  primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary         ->  NUMBER | "true" | "false"
                    | "(" expression ")" | IDENTIFIER ;

arguments       ->  expression ("," expression)* ;
